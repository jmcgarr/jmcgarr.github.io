plugins {
    id 'org.jbake.site' version '5.0.0'
    id "org.ajoberstar.git-publish" version "1.0.1"
}

bakePreview {
    port = '8080'
}

gitPublish {
  repoUri = 'https://github.com/jmcgarr/jmcgarr.github.io.git'
  branch = 'master'
  commitMessage = 'Publishing an update to my website'
  contents {
    from (file('build/jbake')) {
      into '.'
    }
  }
}

// Based on a nice blog post from LD: http://ldaley.com/post/164424162157/auto-reload-html-asciidoctor-gradle-plugin-output
task liveEdit {
    if (System.getProperty("os.name").toLowerCase().contains("mac")) {
        doLast {
            ["osascript", "-e", """
                try
                    tell application "Finder" to get application file id "com.google.Chrome"
                    set appExists to true
                on error
                    set appExists to false
                end try
                if appExists then
                    tell application "Google Chrome"
                        set theUrl to "http://localhost:8080/"
                        set found to false
                        set theWindowIndex to 0
                        repeat with theWindow in every window
                            set theTabIndex to 0
                            repeat with theTab in every tab of theWindow
                                set theTabIndex to theTabIndex + 1
                                if theTab's URL starts with theUrl then
                                    set found to true
                                    tell application "Google Chrome" to set active tab index of theWindow to theTabIndex
                                    tell theTab to reload
                                end if
                            end repeat
                        end repeat
                        if not found then
                            set URL of (active tab of (make new window)) to theUrl
                        end if
                    end tell
                end if
            """].execute()
        }
    }
}

bakePreview.dependsOn tasks.find{ it.name == 'bake' }
gitPublishCommit.dependsOn tasks.find{ it.name == 'bake' }
